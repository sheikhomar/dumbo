"Name"    = 'The Z programming language'
"Version" = '0.1'
"Author"  = 'Group sw401f16'
"About"   = ''
"Case Sensitive" = 'False'
"Start Symbol" = <Start>
               
! GoldParsers way to make comments: http://www.goldparser.org/doc/grammars/example-group.htm
Comment Block @= { Nesting = All, Advance = Character }
Comment Start   = '*/'
Comment End     = '/*'
Comment Line    = '//'              

! Set Specification
{WS} = {WhiteSpace} - {CR} - {LF}
               
! Terminals specifications
Id = ({Letter} | '_'){AlphaNumeric}*
WhiteSpace = {WS}+
EOL = ({CR}{LF}{WS}* | {CR}{WS}* | {LF}{WS}*)+
Assign = ':='


NumberLiteral = {Number}+ '.' {Number}+ | '.' {Number}+ | {Number}+
TextLiteral = '"' {AlphaNumeric}* '"'
BooleanLiteral = 'TRUE' | 'FALSE'

! CFG               
<Start> ::=  'Program' EOL <Stmts> 'End Program'
               
<Literal> ::= NumberLiteral
           | TextLiteral
           | BooleanLiteral

! Declerations    

<Decl> ::= <NumberDecl> 
         | <TextDecl> 
         | <BoolDecl> 
     
<NumberDecl> ::= 'Number' Id 
              | 'Number' Id Assign NumberLiteral 
              
<TextDecl> ::= 'Text' Id
            | 'Text' Id Assign TextLiteral
            
<BoolDecl> ::= 'Boolean' Id
            | 'Boolean' Id Assign Booleanliteral
                     

! Statements
<Stmts> ::= <Stmt> EOL <Stmts>
         | <>

<Stmt> ::= <AssignStmt> 
        | <IfStmt> 
        | <Decl>
      
<AssignStmt> ::= Id Assign <Expr>
     
<IfStmt> ::= 'if' '(' <Expr> ')' 'then' EOL <Stmts> <ElseStmt> 'end if'

<ElseStmt> ::= 'else' EOL <Stmts>
            | <>

! Operators      
<Expr> ::= <Expr> 'OR' <Logical-AND>
         | <Logical-AND>

<Logical-AND> ::= <Logical-AND> 'AND' <Equality>
          | <Equality>

<Equality> ::= <Equality> '=' <Relational>
             | <Relational>

<Relational> ::= <Relational> '<' <Additive>
               | <Relational> '>' <Additive>
               | <Relational> '<=' <Additive>
               | <Relational> '>=' <Additive>
               | <Additive>

<Additive> ::= <Additive> '+' <Multiplicative>
             | <Additive> '-' <Multiplicative>
             | <Multiplicative>
             
<Multiplicative> ::= <Multiplicative> '*' <Unary>
                   | <Multiplicative> '/' <Unary>
                   | <Multiplicative> '%' <Unary>
                   | <Unary>

<Unary> ::= 'NOT' <Unary>
         | '-' <Unary>
         | <Value>

<Value> ::= <Literal>
          | Id
          | '(' <Expr> ')'
