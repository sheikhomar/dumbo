"Name"    = 'The Z programming language'
"Version" = '0.1'
"Author"  = 'Group sw401f16'
"About"   = ''
"Case Sensitive" = 'False'
"Start Symbol" = <Start>
               
! Known Isues: 
! - Some Statements needs exactly one space between its terminals
! - Else if requires at least one space between else and if
! - Support for escape characters in Text literal
! - Support for optional parantheses in if-statements

! GoldParsers way to make comments: http://www.goldparser.org/doc/grammars/example-group.htm
Comment Block @= { Nesting = All, Advance = Character }
Comment Start   = '*/'
Comment End     = '/*'
Comment Line    = '//'              

! Set Specification
{WS} = {WhiteSpace} - {CR} - {LF}
               
! Terminals specifications
Id = ({Letter} | '_'){AlphaNumeric}*
WhiteSpace = {WS}+
NewLine = ({CR}{LF}|{CR})+
Assign = ':='
{CharacterSet} = {All Valid} - {&22}

NumberLiteral = {Number}+ '.' {Number}+ | '.' {Number}+ | {Number}+
TextLiteral = '"' {CharacterSet}* '"'
BooleanLiteral = 'TRUE' | 'FALSE'

! CFG               

<Start> ::=  <Program> <nl> <FuncDecls>
         | <Program>
     
<Program> ::= 'Program' <nl> <Stmts> 'End Program'
          
<Literal> ::= NumberLiteral
           | TextLiteral
           | BooleanLiteral
           
<MultiId> ::= ',' Id <MultiId>
           | <>

<Id> ::= Id <MultiId>
      
<nl>     ::= NewLine <nl>
           | NewLine

! Declerations    

<Decl> ::= <NumberDecl> 
         | <TextDecl> 
         | <BoolDecl> 
     
<NumberDecl> ::= 'Number' Id 
              | 'Number' Id Assign NumberLiteral 
              
<TextDecl> ::= 'Text' Id
            | 'Text' Id Assign TextLiteral
            
<BoolDecl> ::= 'Boolean' Id
            | 'Boolean' Id Assign Booleanliteral
                     
! Statements 

<Stmts> ::= <Stmt> <nl> <Stmts>   ! Bug in language -- We must always have EOL before end program
         | <>

<Stmt> ::= <AssignStmt> 
        | <IfStmt> 
        | <RepeatStmt>
        | <Decl>
        | <ReturnStmt>
        | <FuncCall>
        | <nl>
        
<AssignStmt> ::= <Id> Assign <Expr>
     
<IfStmt> ::= 'if' '(' <Expr> ')' 'then' <nl> <Stmts> <ElseIfStmts> <ElseStmt> 'end if'

<ElseStmt> ::= 'else' <nl> <Stmts>
            | <>
            
<ElseIfStmts> ::= <ElseIfStmt> <ElseIfStmts>
               | <>

<ElseIfStmt> ::= 'else if' '(' <Expr> ')' 'then' <nl> <Stmts>

<RepeatStmt> ::= 'Repeat' '(' NumberLiteral ')' <nl> <Stmts> 'End Repeat'
              | 'Repeat' 'While' '(' <Expr> ')' <nl> <Stmts> 'End Repeat'

<FuncCall> ::= Id '(' <ActualParams> ')'
            
<ActualParams> ::= <Expr> <MultiActualParams>
                | <>
                
<MultiActualParams> ::= ',' <Expr> <MultiActualParams>
                     | <>
                     
<ReturnStmt> ::= 'Return' <ReturnValues>
              | 'Returns' <ReturnValues>

              
<ReturnValues> ::= <ReturnValue>
                | <ReturnValue> ',' <ReturnValues>
                
<ReturnValue> ::= <Expr>
               | 'Nothing'

! Functions

<FuncDecls> ::= <FuncDecl> <FuncDecls>
             | <>

<FuncDecl> ::= 'Function' Id '(' <FormalParams> ')' 'Returns' <ReturnTypes> <nl> <Stmts> 'End Function'
            
<FormalParams> ::= <FormalParam> <MultiFormalParams>
                | <>

<MultiFormalParams> ::= ',' <FormalParam> <MultiFormalParams>
                     | <>

<FormalParam> ::= 'Number' Id
         | 'Text' Id
         | 'Boolean' Id
         
<ReturnTypes> ::= 'Nothing'
               | <ReturnType> <MultiReturnTypes>

<MultiReturnTypes> ::= ',' <ReturnType> <MultiReturnTypes>
                    | <>
              
<ReturnType> ::= 'Number'
              | 'Text' 
              | 'Boolean'


! Operators    
  
<Expr> ::= <Expr> 'OR' <Logical-AND>
         | <Logical-AND>

<Logical-AND> ::= <Logical-AND> 'AND' <Equality>
          | <Equality>

<Equality> ::= <Equality> '=' <Relational>
             | <Relational>

<Relational> ::= <Relational> '<' <Additive>
               | <Relational> '>' <Additive>
               | <Relational> '<=' <Additive>
               | <Relational> '>=' <Additive>
               | <Additive>

<Additive> ::= <Additive> '+' <Multiplicative>
             | <Additive> '-' <Multiplicative>
             | <Multiplicative>
             
<Multiplicative> ::= <Multiplicative> '*' <Unary>
                   | <Multiplicative> '/' <Unary>
                   | <Multiplicative> '%' <Unary>
                   | <Unary>

<Unary> ::= 'NOT' <Unary>
         | '-' <Unary>
         | <Value>

<Value> ::= <Literal>
          | <FuncCall>
          | Id
          | '(' <Expr> ')'
